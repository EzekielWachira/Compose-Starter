/*
 * Copyright 2023 Atick Faisal
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
    id("dagger.hilt.android.plugin")
    id("androidx.navigation.safeargs.kotlin")
    id("kotlin-kapt")
}

android {
    def date = new Date()
    def formattedDate = date.format("dd_MM_yyyy_hh_mm_a")

    compileSdk rootProject.ext.compile_sdk_version

    defaultConfig {
        applicationId rootProject.ext.app_id
        minSdk rootProject.ext.min_sdk_version
        targetSdk rootProject.ext.target_sdk_version
        versionCode rootProject.ext.version_code
        versionName rootProject.ext.version_name
    }

    buildTypes {
        debug {
            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName =
                            "${rootProject.name.replace(" ", "_") + "_"}" +
                                    "${variant.buildType.name + "_v"}" +
                                    "${variant.versionName + "_"}" +
                                    "${formattedDate}.apk"
                }
            }
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"),
                    "proguard-rules.pro"

            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName =
                            "${rootProject.name.replace(" ", "_") + "_"}" +
                                    "${variant.buildType.name + "_v"}" +
                                    "${variant.versionName + "_"}" +
                                    "${formattedDate}.apk"
                }
            }
        }
    }
    signingConfigs {
        release {
            def tmpFilePath = System.getProperty("user.home") + "/work/_temp/keystore/"
            def allFilesFromDir = new File(tmpFilePath).listFiles()

            if (allFilesFromDir != null) {
                def keystoreFile = allFilesFromDir.first()
                keystoreFile.renameTo("key/key.jks")
            }

            storeFile = file("key/key.jks")
            storePassword System.getenv("SIGNING_STORE_PASSWORD")
            keyAlias System.getenv("SIGNING_KEY_ALIAS")
            keyPassword System.getenv("SIGNING_KEY_PASSWORD")
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
        freeCompilerArgs = freeCompilerArgs + [
                "-opt-in=kotlin.RequiresOptIn",
                "-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi",
                "-opt-in=kotlinx.coroutines.FlowPreview",
                "-opt-in=androidx.compose.material3.ExperimentalMaterial3Api"
        ]
    }
    buildFeatures {
        // ... BuildConfig (https://stackoverflow.com/a/74692390/12737399)
        buildConfig = true
        compose = true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_compiler_version
    }
    packagingOptions {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }
    namespace("dev.atick.compose")
}

dependencies {
    implementation(project(path: ":core:ui"))
    implementation(project(path: ":network"))
    implementation(project(path: ":storage:room"))
    implementation(project(path: ":storage:preferences"))

    // ... Dagger-Hilt
    implementation("com.google.dagger:hilt-android:$hilt_version")
    kapt("com.google.dagger:hilt-android-compiler:$hilt_version")
}

kapt {
    correctErrorTypes = true
}